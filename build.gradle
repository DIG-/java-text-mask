// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    final Properties localProp = new Properties()
    def localPropFile = (new File(rootDir, "local.properties"))
    if (localPropFile.exists()) {
        localPropFile.withInputStream { localProp.load(it) }
    }
    def findPropertyOrEnvironment = { String key ->
        findProperty(key) ?: localProp.getProperty(key) ?: System.getenv(key)
    }

    // This file must be the text version (--armor)
    String signingFilename = findPropertyOrEnvironment("GPG_SIGNING_KEY_FILE")
    File signingFile = null
    if (signingFilename != null) {
        signingFile = file(signingFilename)
        if (!signingFile.exists()) {
            signingFile = new File(rootDir, signingFilename)
            if (!signingFile.exists()) {
                signingFile = null
            }
        }
    }

    ext.isAndroidEnabled = !(new File(rootDir, "disable-android.gradle")).exists()
    ext.deploy = [
            "central": [
                    "enabled" : findPropertyOrEnvironment("MVN_CENTRAL_USERNAME") != null,
                    "username": findPropertyOrEnvironment("MVN_CENTRAL_USERNAME"),
                    "password": findPropertyOrEnvironment("MVN_CENTRAL_PASSWORD"),
            ],
            "signing": [
                    "enabled" : signingFile != null && findPropertyOrEnvironment("GPG_SIGNING_KEY_ID") != null && findPropertyOrEnvironment("GPG_SIGNING_PASSWORD") != null,
                    "secret"  : signingFile?.getText("UTF-8"),
                    "key_id"  : findPropertyOrEnvironment("GPG_SIGNING_KEY_ID"),
                    "password": findPropertyOrEnvironment("GPG_SIGNING_PASSWORD"),
            ]
    ]
    ext.versions = [
            "java"   : JavaVersion.VERSION_1_8,
            "mask"   : [
                    "major": "1",
                    "minor": "0",
                    "pkg"  : "",
            ],
            "android": [
                    "minimum": 21,
                    "compile": 33,
                    "target" : 33,
            ],
    ]
    ext.versions.mask.pkg = "${ext.versions.mask.major}.${ext.versions.mask.minor}."
}
plugins {
    id "com.android.application" version "7.4.2" apply false
    id "com.android.library" version "7.4.2" apply false
    id "org.jetbrains.kotlin.android" version "1.8.10" apply false
}